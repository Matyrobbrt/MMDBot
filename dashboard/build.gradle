import java.time.OffsetDateTime
import java.time.ZoneOffset

plugins {
    id 'application'
    id 'java-library'
    // id 'maven'
    id 'eclipse'
    id 'idea'
    alias(libs.plugins.spotbugs)
    alias(libs.plugins.shadow)
    alias(libs.plugins.licenser)
    // alias(libs.plugins.lombok)
}

sourceSets {
    common {}
    client {}
    server {}
}

configurations {
    global
    commonImplementation.extendsFrom(global)
    clientImplementation.extendsFrom(global)
    serverImplementation.extendsFrom(global)

    clientImplementation.extendsFrom(commonImplementation)
    serverImplementation.extendsFrom(commonImplementation)
}

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

repositories {
    mavenCentral()
}

license {
    header = rootProject.file('LicenseHeader.md')
    include '**/*.java'
    include '**/*.kt'
    exclude '**/package-info.java'
    exclude '**/module-info.java'
    newLine = false
    properties {
        year = OffsetDateTime.now(ZoneOffset.UTC).withNano(0).getYear()
    }
}

dependencies {
    global libs.netty
    global libs.guava
    global libs.slf4japi
    global libs.fastutil

    serverImplementation sourceSets.common.output
    clientImplementation sourceSets.common.output

    api sourceSets.server.output
    api sourceSets.common.output
}
